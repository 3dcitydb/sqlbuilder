plugins {
    id 'java'
    id 'maven-publish'
}

group 'org.citydb'
version '3.0.0'
description 'Dynamic SQL builder for the 3D City Database'

ext {
    date = new Date()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withJavadocJar()
    withSourcesJar()
}

tasks.named('compileJava') {
    options.javaModuleVersion = provider { project.version }
}

repositories {
    mavenCentral()
}

javadoc {
    options {
        title = "$project.name $project.version"
        header = title
        overview "resources/javadoc/overview.html"
        bottom = file("resources/javadoc/bottom.html").text.replaceAll("@date@", "${date.format('yyyy')}")
        addStringOption('doctitle', project.name + ' - ' + project.version)
        addStringOption('Xdoclint:none', '-quiet')
    }
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
    into('META-INF') {
        from 'LICENSE'
    }
}

publishing {
    repositories {
        maven {
            url project.hasProperty('gitLab_maven_url') ? project.property('gitLab_maven_url') : System.getenv('GITLAB_MAVEN_URL')
            name 'gitlab-maven'
            credentials(HttpHeaderCredentials) {
                name = 'Private-Token'
                value = project.hasProperty('gitLab_private_token') ? project.property('gitLab_private_token') : System.getenv('GITLAB_PRIVATE_TOKEN')
            }
            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = project.name
                description = project.description
                url = 'https://github.com/3dcitydb'
                scm {
                    url = 'https://github.com/3dcitydb/sqlbuilder'
                    connection = 'scm:git:https://github.com/3dcitydb/sqlbuilder.git'
                }
            }
        }
    }
}
